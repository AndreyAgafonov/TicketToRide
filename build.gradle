buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.71'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.71'
}
repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://kotlin.bintray.com/kotlin-js-wrappers/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    mavenCentral()
}

def ktor_version = '1.3.2'
def logback_version = '1.2.3'
def kotlinx_html_version = '0.7.1'
def serialization_version = '0.20.0'
def kotest_version = '4.1.0.268-SNAPSHOT'

kotlin {
    jvm()
    js {
        browser {}
        useCommonJs()
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains:kotlin-css:1.0.0-pre.94-kotlin-1.3.70"
                implementation "org.jetbrains:kotlin-css-jvm:1.0.0-pre.94-kotlin-1.3.70"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
                implementation "io.ktor:ktor-server-core:$ktor_version"
                implementation "io.ktor:ktor-server-netty:$ktor_version"
                implementation "io.ktor:ktor-html-builder:$ktor_version"
                implementation "io.ktor:ktor-websockets:$ktor_version"
                implementation "ch.qos.logback:logback-classic:$logback_version"
            }
        }
        jvmTest {
            dependencies {
                implementation "io.kotest:kotest-runner-console-jvm:$kotest_version"
                implementation "io.kotest:kotest-assertions-core-jvm:$kotest_version"
                implementation "io.kotest:kotest-property-jvm:4.0.5"
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation npm('react', '16.13.0')
                implementation npm('react-dom', '16.13.0')
                implementation "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version"
                implementation "org.jetbrains:kotlin-styled:1.0.0-pre.94-kotlin-1.3.70"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
                implementation "com.ccfraser.muirwik:muirwik-components:0.4.1"
                implementation npm('@material-ui/core', '4.9.8')
                implementation npm('styled-components', '5.0.1')
                implementation npm('inline-style-prefixer', '6.0.0')
                implementation npm('google-map-react', '1.1.7')
            }
        }
        jsTest {
            dependencies {
                implementation "io.kotest:kotest-core-js:$kotest_version"
                implementation "io.kotest:kotest-assertions-core-js:$kotest_version"
                implementation "io.kotest:kotest-property-js:$kotest_version"
            }
        }
        all {
            languageSettings {
                enableLanguageFeature("InlineClasses")
                useExperimentalAnnotation("kotlin.ExperimentalUnsignedTypes")
                useExperimentalAnnotation("kotlinx.coroutines.ExperimentalCoroutinesApi")
                useExperimentalAnnotation("kotlinx.serialization.UnstableDefault")
            }
        }
    }
}

jvmJar {
    dependsOn(jsBrowserDevelopmentWebpack)
    from(jsBrowserDevelopmentWebpack.outputFile)
}

task run(type: JavaExec, dependsOn: [jvmJar]) {
    group = "application"
    main = "ticketToRide.ServerKt"
    classpath(configurations.jvmRuntimeClasspath, jvmJar)
    args = []
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}